module Flow.Parser.PatternSpec (spec) where

import "base" Data.Functor ((<&>))
import "base" Data.List.NonEmpty qualified as ListNE
import "base" Data.Maybe (fromJust)
import "base" GHC.Stack (HasCallStack)
import "hspec" Test.Hspec (Spec, describe, it)
import "nonempty-vector" Data.Vector.NonEmpty qualified as NE
import "text" Data.Text (Text)
import "vector" Data.Vector qualified as Vector

import Flow.AST.Surface qualified as Surface
import Flow.AST.Surface.Common qualified as Surface
import Flow.AST.Surface.Constraint qualified as Surface
import Flow.AST.Surface.Literal qualified as Surface
import Flow.AST.Surface.Pattern qualified as Surface
import Flow.Lexer qualified as Lexer
import Flow.Parser (pPattern)
import Flow.Parser.Common (Parser)
import Flow.Parser.SpecHelpers (testParser, shouldBeParsed, shouldBe)
import Flow.Parser.Pattern qualified as PPat

pPatternSimple :: Parser (Surface.PatternSimple Lexer.SourceSpan)
pPatternSimple =
  PPat.pPatternSimple pPatternSimple (fail "anyType") <&> uncurry Surface.PatternSimple

anyType :: Surface.SimpleTypeIdentifier () -> Surface.AnyTypeIdentifier ty ()
anyType ident =
  Surface.AnyTypeIdentifier
    { qualifierPrefix = Nothing
    , qualifier = Nothing
    , typeQualifier = Nothing
    , identifier = ident
    , ann = ()
    }

mkVar :: Text -> Surface.SimpleVarIdentifier ()
mkVar name = Surface.SimpleVarIdentifier{name, ann = ()}

wrapSimple ::
  Surface.PatternSimpleF Surface.PatternSimple Surface.Type () ->
  Surface.PatternSimple ()
wrapSimple simple =
  Surface.PatternSimple
    { patternSimple = simple
    , ann = ()
    }

wrapPattern ::
  Surface.PatternF Surface.Pattern Surface.Type () ->
  Surface.Pattern ()
wrapPattern pattern =
  Surface.Pattern
    { pattern = pattern
    , ann = ()
    }

wildcardPattern :: Surface.PatternSimple ()
wildcardPattern = wrapSimple Surface.PatSimWildcardF

literalBoolPattern :: Bool -> Surface.Pattern ()
literalBoolPattern value =
  wrapPattern (Surface.PatLiteralF (Surface.LitBool value))

literalIntPattern :: Integer -> Surface.Pattern ()
literalIntPattern value =
  wrapPattern (Surface.PatLiteralF (Surface.LitInteger value))

varPattern :: Text -> Surface.Pattern ()
varPattern name =
  wrapPattern $
    Surface.PatSimpleF $
      Surface.PatSimVarF
        ( Surface.PatternVariableF
            { ref = Nothing
            , mut = Nothing
            , name = mkVar name
            , ann = ()
            }
        )

tuplePattern :: [Surface.Pattern ()] -> Surface.Pattern ()
tuplePattern ps =
  wrapPattern $
    Surface.PatSimpleF $
      Surface.PatSimTupleF (requireVector "tuplePattern" ps)

constructorPattern ::
  Text ->
  Maybe [Surface.SimpleTypeIdentifier ()] ->
  Maybe (Surface.PatternFieldsF Surface.Pattern Surface.Type ()) ->
  Surface.Pattern ()
constructorPattern name params fields =
  wrapPattern $
    Surface.PatSimpleF
      ( Surface.PatSimConstructorAppF
          Surface.PatternConsturctorAppF
            { name = anyType Surface.SimpleTypeIdentifier{name, ann = ()}
            , typeParams =
                params <&> \params' ->
                  Surface.BindersF
                    { regions = mempty
                    , types =
                        Vector.fromList $
                          params' <&> \param'' ->
                            Surface.BinderWoConstraintF
                              { name = param''
                              , kindShort = Nothing
                              , typeType = Nothing
                              , ann = ()
                              }
                    , ann = ()
                    }
            , fields = (,()) <$> fields
            , ann = ()
            }
      )

fieldsTuple ::
  [Surface.Pattern ()] ->
  Surface.PatternFieldsF Surface.Pattern Surface.Type ()
fieldsTuple ps =
  Surface.PatFldsUnnamedF
    ( requireVector
        "fieldsTuple"
        ( ps <&> \p ->
            Surface.PatternFieldUnnamedF
              { value = p
              , optional = Nothing
              , ann = ()
              }
        )
    )

fieldsNamed ::
  [Surface.PatternFieldNamedF Surface.Pattern Surface.Type ()] ->
  Surface.PatternFieldsF Surface.Pattern Surface.Type ()
fieldsNamed ps = Surface.PatFldsNamedF (fromJust $ NE.fromList ps)

mkFieldNamed :: Text -> Surface.Pattern () -> Surface.PatternFieldNamedF Surface.Pattern Surface.Type ()
mkFieldNamed name value =
  Surface.PatFldNmdValueF $
    Surface.PatternFieldNamedValueF
      { name = mkVar name
      , value = value
      , ann = ()
      }

mkFieldNamedPun :: Text -> Surface.PatternFieldNamedF Surface.Pattern Surface.Type ()
mkFieldNamedPun name =
  Surface.PatFldNmdPunningF $
    Surface.PatternFieldNamedPunningF
      { ref = Nothing
      , mut = Nothing
      , name = mkVar name
      , optional = Nothing
      , ann = ()
      }

requireList :: (HasCallStack) => String -> [a] -> ListNE.NonEmpty a
requireList label [] = error (label <> ": expected non-empty list")
requireList _label (x : xs) = x ListNE.:| xs

requireVector :: (HasCallStack) => String -> [a] -> NE.NonEmptyVector a
requireVector label = NE.fromNonEmpty . requireList label

spec :: Spec
spec = describe "Pattern parser (minimal subset)" do
  it "parses wildcard _" do
    testParser "_" pPatternSimple $ shouldBeParsed (`shouldBe` wildcardPattern)

  it "parses literal true" do
    testParser "true" pPattern $ shouldBeParsed (`shouldBe` literalBoolPattern True)

  it "parses variable x" do
    testParser "x" pPattern $ shouldBeParsed (`shouldBe` varPattern "x")

  it "parses tuple (x, y)" do
    testParser "(x, y)" pPattern $ shouldBeParsed (`shouldBe` tuplePattern [varPattern "x", varPattern "y"])

  it "parses constructor without arguments None" do
    let expected =
          constructorPattern
            "None"
            Nothing
            Nothing
    testParser "None" pPattern $ shouldBeParsed (`shouldBe` expected)

  it "parses constructor Some(1)" do
    let expected =
          constructorPattern
            "Some"
            Nothing
            (Just (fieldsTuple [literalIntPattern 1]))
    testParser "Some(1)" pPattern $ shouldBeParsed (`shouldBe` expected)

  it "parses constructor with named fields Cons { head = 1, tail = xs }" do
    let expected =
          constructorPattern
            "Cons"
            Nothing
            ( Just
                ( fieldsNamed
                    [ mkFieldNamed "head" (literalIntPattern 1)
                    , mkFieldNamed "tail" (varPattern "xs")
                    ]
                )
            )
    testParser "Cons { head = 1, tail = xs }" pPattern $ shouldBeParsed (`shouldBe` expected)

  it "parses constructor with fields punning Cons { head, tail }" do
    let expected =
          constructorPattern
            "Cons"
            Nothing
            (Just (fieldsNamed [mkFieldNamedPun "head", mkFieldNamedPun "tail"]))
    testParser "Cons { head, tail }" pPattern $ shouldBeParsed (`shouldBe` expected)
