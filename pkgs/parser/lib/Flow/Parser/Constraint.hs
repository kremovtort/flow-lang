{-# OPTIONS_GHC -Wno-unrecognised-pragmas #-}

{-# HLINT ignore "Use <$>" #-}
module Flow.Parser.Constraint where

import "base" Control.Monad (unless, when)
import "base" Data.Functor (void)
import "base" Data.Maybe (fromJust)
import "megaparsec" Text.Megaparsec qualified as Megaparsec
import "nonempty-vector" Data.Vector.NonEmpty qualified as NonEmptyVector
import "vector" Data.Vector qualified as Vector

import Flow.AST.Surface.Common qualified as Surface
import Flow.AST.Surface.Constraint qualified as Surface
import Flow.Lexer qualified as Lexer
import Flow.Parser.Common (
  HasAnn,
  Parser,
  moduleIdentifier,
  regionIdentifier,
  simpleTypeIdentifier,
  simpleVarIdentifier,
  single,
 )

pBindersApp ::
  (HasAnn ty Lexer.SourceSpan) =>
  Parser (ty Lexer.SourceSpan) ->
  Parser (Surface.BindersAppF ty Lexer.SourceSpan)
pBindersApp pTy = Megaparsec.label "binders app" do
  tokS <- single (Lexer.Punctuation Lexer.LessThan)
  elems <- Megaparsec.sepEndBy1 pTy (single (Lexer.Punctuation Lexer.Comma))
  tokE <- single (Lexer.Punctuation Lexer.GreaterThan)
  pure
    Surface.BindersAppF
      { types = fromJust $ NonEmptyVector.fromList elems
      , ann = Lexer.SourceSpan tokS.span.start tokE.span.end
      }

anyTypeIdentifier ::
  forall ty.
  (HasAnn ty Lexer.SourceSpan) =>
  Parser (ty Lexer.SourceSpan) ->
  Parser (Surface.AnyTypeIdentifier ty Lexer.SourceSpan)
anyTypeIdentifier pTy = do
  qualifier <- Megaparsec.many (Megaparsec.try (moduleIdentifier <* moduleSeparator))
  typeQualifier <- Megaparsec.optional $ Megaparsec.try do
    typeName <- simpleTypeIdentifier
    typeParams <- pBindersApp pTy
    _ <- single (Lexer.Punctuation Lexer.ColonColon)
    pure
      Surface.TypeQualifierF
        { typeName
        , typeParams
        }
  identifier <- simpleTypeIdentifier
  pure $
    Surface.AnyTypeIdentifier
      { qualifier = NonEmptyVector.fromList qualifier
      , typeQualifier
      , identifier
      , ann =
          Lexer.SourceSpan
            { start = case qualifier of
                [] -> identifier.ann.start
                q : _ -> q.ann.start
            , end = identifier.ann.end
            }
      }
 where
  moduleSeparator = single (Lexer.Punctuation Lexer.ColonColon)

anyVarIdentifier ::
  (HasAnn ty Lexer.SourceSpan) =>
  Parser (ty Lexer.SourceSpan) ->
  Parser (Surface.AnyVarIdentifier ty Lexer.SourceSpan)
anyVarIdentifier pTy = do
  qualifier <- Megaparsec.many (Megaparsec.try (moduleIdentifier <* moduleSeparator))
  typeQualifier <- Megaparsec.optional do
    typeName <- simpleTypeIdentifier
    typeParams <- pBindersApp pTy
    pure
      Surface.TypeQualifierF
        { typeName
        , typeParams
        }
  identifier <- simpleVarIdentifier
  pure $
    Surface.AnyVarIdentifier
      { qualifier = NonEmptyVector.fromList qualifier
      , typeQualifier = typeQualifier
      , identifier = identifier
      , ann =
          Lexer.SourceSpan
            { start = case qualifier of
                [] -> identifier.ann.start
                q : _ -> q.ann.start
            , end = identifier.ann.end
            }
      }
 where
  moduleSeparator = single (Lexer.Punctuation Lexer.ColonColon)

pKindTreeRoot ::
  (HasAnn ty Lexer.SourceSpan) =>
  Parser (ty Lexer.SourceSpan) ->
  Parser (Surface.KindTreeRootF ty Lexer.SourceSpan, Lexer.SourceSpan)
pKindTreeRoot pTy = do
  tokS <- single (Lexer.Punctuation Lexer.LessThan)
  trees <- Megaparsec.sepEndBy1 pKindTree (single (Lexer.Punctuation Lexer.Comma))
  tokE <- single (Lexer.Punctuation Lexer.GreaterThan)
  pure
    ( fromJust $ NonEmptyVector.fromList trees
    , Lexer.SourceSpan
        { start = tokS.span.start
        , end = tokE.span.end
        }
    )
 where
  pKindTree =
    Megaparsec.choice
      [ Megaparsec.try pKindParams
      , pKindHole
      ]

  pKindHole = do
    tokS <- single (Lexer.Punctuation Lexer.Underscore)
    typeType <- Megaparsec.optional do
      _ <- single (Lexer.Punctuation Lexer.Colon)
      pTy
    pure $
      Surface.KTHoleF
        Surface.KindHoleF
          { holeAnn = tokS.span
          , typeType = typeType
          , ann =
              Lexer.SourceSpan
                { start = tokS.span.start
                , end = case typeType of
                    Just ty -> ty.ann.end
                    Nothing -> tokS.span.end
                }
          }
  pKindParams = do
    tokS <- single (Lexer.Punctuation Lexer.Underscore)
    _ <- single (Lexer.Punctuation Lexer.LessThan)
    params <- Megaparsec.sepEndBy1 pKindTree (single (Lexer.Punctuation Lexer.Comma))
    tokE <- single (Lexer.Punctuation Lexer.GreaterThan)
    typeType <- Megaparsec.optional do
      _ <- single (Lexer.Punctuation Lexer.Colon)
      pTy
    pure $
      Surface.KTParamsF
        Surface.KindParamsF
          { holeAnn = tokS.span
          , params = fromJust $ NonEmptyVector.fromList params
          , typeType
          , ann =
              Lexer.SourceSpan
                { start = tokS.span.start
                , end = case typeType of
                    Just ty -> ty.ann.end
                    Nothing -> tokE.span.end
                }
          }

pBindersWoConstraints ::
  forall ty.
  (HasAnn ty Lexer.SourceSpan) =>
  Parser (ty Lexer.SourceSpan) ->
  Parser (Surface.BindersWoConstraintsF ty Lexer.SourceSpan)
pBindersWoConstraints pTy = Megaparsec.label "binders with constraints" do
  tokS <- single (Lexer.Punctuation Lexer.LessThan)
  regionBinders <- Megaparsec.sepBy regionIdentifier (single (Lexer.Punctuation Lexer.Comma))
  unless (null regionBinders) do
    void $ single (Lexer.Punctuation Lexer.Comma)
  typeBinders <- Megaparsec.sepBy pBinderWoConstraints (single (Lexer.Punctuation Lexer.Comma))
  when (null regionBinders && null typeBinders) do
    fail "Expected at least one region or type binder"
  _ <- Megaparsec.optional $ single (Lexer.Punctuation Lexer.Comma)
  tokE <- single (Lexer.Punctuation Lexer.GreaterThan)
  pure
    Surface.BindersF
      { regions = Vector.fromList (fmap Surface.RegionBinderWoConstraintsF regionBinders)
      , types = Vector.fromList typeBinders
      , ann = Lexer.SourceSpan{start = tokS.span.start, end = tokE.span.end}
      }
 where
  pBinderWoConstraints = do
    name <- simpleTypeIdentifier
    kindShort <- Megaparsec.optional (pKindTreeRoot pTy)
    typeType <- Megaparsec.optional do
      _ <- single (Lexer.Punctuation Lexer.Colon)
      pTy
    pure $
      Surface.BinderWoConstraintF
        { name = name
        , kindShort = fst <$> kindShort
        , typeType = typeType
        , ann =
            Lexer.SourceSpan
              { start = name.ann.start
              , end = case typeType of
                  Just ty -> ty.ann.end
                  Nothing -> case kindShort of
                    Just (_, ann) -> ann.end
                    Nothing -> name.ann.end
              }
        }

pBindersWConstraints ::
  forall ty.
  (HasAnn ty Lexer.SourceSpan) =>
  Parser (ty Lexer.SourceSpan) ->
  Parser (Surface.BindersWConstraintsF ty Lexer.SourceSpan)
pBindersWConstraints pTy = Megaparsec.label "binders with constraints" do
  tokS <- single (Lexer.Punctuation Lexer.LessThan)
  regionBinders <- Megaparsec.sepBy pRegionBinderWConstraints (single (Lexer.Punctuation Lexer.Comma))
  unless (null regionBinders) do
    void $ single (Lexer.Punctuation Lexer.Comma)
  typeBinders <- Megaparsec.sepBy pBinderWConstraints (single (Lexer.Punctuation Lexer.Comma))
  when (null regionBinders && null typeBinders) do
    fail "Expected at least one region or type binder"
  _ <- Megaparsec.optional $ single (Lexer.Punctuation Lexer.Comma)
  tokE <- single (Lexer.Punctuation Lexer.GreaterThan)
  pure $
    Surface.BindersF
      { regions = Vector.fromList regionBinders
      , types = Vector.fromList typeBinders
      , ann = Lexer.SourceSpan{start = tokS.span.start, end = tokE.span.end}
      }
 where
  pRegionBinderWConstraints = do
    name <- regionIdentifier
    constraint <- Megaparsec.optional pBinderConstraints
    pure $
      Surface.RegionBinderWConstraintsF
        { name
        , constraint
        , ann =
            Lexer.SourceSpan
              { start = name.ann.start
              , end = case constraint of
                  Just cnst -> cnst.ann.end
                  Nothing -> name.ann.end
              }
        }

  pBinderWConstraints = do
    name <- simpleTypeIdentifier
    kindShort <- Megaparsec.optional (pKindTreeRoot pTy)
    typeType <- Megaparsec.optional do
      _ <- single (Lexer.Punctuation Lexer.Colon)
      pTy
    constraint <- Megaparsec.optional pBinderConstraints
    pure
      Surface.BinderWConstraintsF
        { name = name
        , kindShort = fst <$> kindShort
        , typeType = typeType
        , constraint = constraint
        , ann =
            Lexer.SourceSpan
              { start = name.ann.start
              , end = case constraint of
                  Just cnst -> cnst.ann.end
                  Nothing -> case typeType of
                    Just ty -> ty.ann.end
                    Nothing -> case kindShort of
                      Just (_, ann) -> ann.end
                      Nothing -> name.ann.end
              }
        }

  pBinderConstraints = do
    tokS <- single (Lexer.Punctuation Lexer.ColonLessThan)
    constraints <- Megaparsec.sepBy1 (anyTypeIdentifier pTy) (single (Lexer.Punctuation Lexer.Plus))
    pure
      Surface.BinderConstraintsF
        { constraints = fromJust $ NonEmptyVector.fromList constraints
        , ann =
            Lexer.SourceSpan
              { start = tokS.span.start
              , end = (last constraints).ann.end
              }
        }

pWhereBlockHead ::
  forall ty.
  (HasAnn ty Lexer.SourceSpan) =>
  Parser (ty Lexer.SourceSpan) ->
  Parser (Surface.WhereBlockF ty Lexer.SourceSpan)
pWhereBlockHead pTy = do
  tokS <- single (Lexer.Keyword Lexer.Where)
  clauses <- fromJust . NonEmptyVector.fromList <$> Megaparsec.sepEndBy1 (pWhereClause pTy) (single (Lexer.Punctuation Lexer.Comma))
  pure $
    Surface.WhereBlockF
      { clauses = fmap fst clauses
      , ann =
          Lexer.SourceSpan
            { start = tokS.span.start
            , end = (snd $ NonEmptyVector.last clauses).end
            }
      }

pWhereBlockNested ::
  forall ty.
  (HasAnn ty Lexer.SourceSpan) =>
  Parser (ty Lexer.SourceSpan) ->
  Parser (Surface.WhereBlockF ty Lexer.SourceSpan)
pWhereBlockNested pTy = do
  tokS <- single (Lexer.Keyword Lexer.Where)
  _ <- single (Lexer.Punctuation Lexer.LeftBrace)
  clauses <- fromJust . NonEmptyVector.fromList <$> Megaparsec.sepEndBy1 (pWhereClause pTy) (single (Lexer.Punctuation Lexer.Comma))
  tokE <- single (Lexer.Punctuation Lexer.RightBrace)
  pure $
    Surface.WhereBlockF
      { clauses = fmap fst clauses
      , ann = Lexer.SourceSpan{start = tokS.span.start, end = tokE.span.end}
      }

pWhereClause ::
  forall ty.
  (HasAnn ty Lexer.SourceSpan) =>
  Parser (ty Lexer.SourceSpan) ->
  Parser (Surface.WhereClauseF ty Lexer.SourceSpan, Lexer.SourceSpan)
pWhereClause pTy =
  Megaparsec.choice
    [ pWhereConstraint
    , pWhereAlias
    ]
 where
  pWhereConstraint = do
    ty <- pTy
    pure (Surface.WhereConstraintF ty, ty.ann)
  pWhereAlias = do
    typeDefinition <- pTypeDefinition pTy
    pure (Surface.WhereAliasF typeDefinition, typeDefinition.ann)

pTypeDefinition ::
  forall ty.
  (HasAnn ty Lexer.SourceSpan) =>
  Parser (ty Lexer.SourceSpan) ->
  Parser (Surface.TypeDefinitionF ty Lexer.SourceSpan)
pTypeDefinition pTy = do
  tokS <- single (Lexer.Keyword Lexer.Type)
  name <- simpleTypeIdentifier
  typeParams <- Megaparsec.optional (pBindersWoConstraints pTy)
  _ <- single (Lexer.Punctuation Lexer.Assign)
  type_ <- pTy
  pure $
    Surface.TypeDefinitionF
      { name
      , typeParams
      , type_
      , ann =
          Lexer.SourceSpan
            { start = tokS.span.start
            , end = type_.ann.end
            }
      }
